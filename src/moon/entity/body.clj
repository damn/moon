(ns moon.entity.body
  (:require [clojure.math.vector2 :as v]
            [moon.body :as body]
            [moon.rectangle :as rectangle]
            [qrecord.core :as q])
  (:import (com.badlogic.gdx.math Intersector
                                  Rectangle)))

(q/defrecord Body [body/position
                   body/width
                   body/height
                   body/collides?
                   body/z-order
                   body/rotation-angle]
  body/Body
  (rectangle [_]
    (let [[x y] [(- (position 0) (/ width  2))
                 (- (position 1) (/ height 2))]]
      (Rectangle. x y width height)))

  (touched-tiles [_]
    (rectangle/touched-tiles
     {:x (- (position 0) (/ width  2))
      :y (- (position 1) (/ height 2))
      :width  width
      :height height}))

  (overlaps? [body other-body]
    (Intersector/overlaps (body/rectangle body)
                          (body/rectangle other-body)))

  (distance [body other-body]
    (v/distance (:body/position body)
                (:body/position other-body)))

  (direction [body other-body]
    (v/direction (:body/position body)
                 (:body/position other-body))))


(defn create
  [[_k
    {[x y] :position
     :keys [position
            width
            height
            collides?
            z-order
            rotation-angle]}]
   {:keys [ctx/minimum-size
           ctx/z-orders]}]
  (assert position)
  (assert width)
  (assert height)
  (assert (>= width  (if collides? minimum-size 0)))
  (assert (>= height (if collides? minimum-size 0)))
  (assert (or (boolean? collides?) (nil? collides?)))
  (assert ((set z-orders) z-order))
  (assert (or (nil? rotation-angle)
              (<= 0 rotation-angle 360)))
  (map->Body
   {:position (mapv float position)
    :width  (float width)
    :height (float height)
    :collides? collides?
    :z-order z-order
    :rotation-angle (or rotation-angle 0)}))
